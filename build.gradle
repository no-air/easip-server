plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.cloud.tools.jib' version '3.4.0'
}

group = 'com.noair'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'com.github.f4b6a3:ulid-creator:5.2.2'

	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'com.google.api-client:google-api-client:1.32.1'
	implementation 'com.nimbusds:nimbus-jose-jwt:9.37'

	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	implementation 'mysql:mysql-connector-java:8.0.33'
	
//	 fcm
	implementation 'com.google.firebase:firebase-admin:9.2.0'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

clean {
	delete file('src/main/generated')
}

jib {
	def imageTag = System.getenv("IMAGE_TAG")
	def serverPort = "8080"
	def activeProfile = System.getenv("ACTIVE_PROFILE")
	def imageName = System.getenv("IMAGE_NAME")
	from {
		image = 'eclipse-temurin:21-jdk'
	}
	to {
		image = imageName + ':latest'
		tags = ['latest', imageTag]
	}
	container {
		jvmFlags = [
				'-Dspring.profiles.active=' + activeProfile,
				'-Dserver.port=' + serverPort,
				'-Xms3400M',
				'-Xmx3400M',
				'-XX:+UseG1GC',
				'-XX:+UseContainerSupport',
				'-XX:MaxGCPauseMillis=400',
				'-XX:+DisableExplicitGC',
				'-XX:+AlwaysPreTouch', //미리 영역 채우기
				'-Duser.timezone=Asia/Seoul', //타임존
				'-Dfile.encoding=UTF-8',
				'-server'
		]
		ports = [serverPort]
	}
}

